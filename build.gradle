group 'com.sample'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.51'
    ext.ktor_version = '0.9.3'

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

mainClassName = 'application.MainKt'

sourceSets {
    main.kotlin.srcDirs = [ 'src/main' ]
    main.java.srcDirs = [ 'src/main']
    test.kotlin.srcDirs = [ 'src/test' ]
}

repositories {
    jcenter()
    mavenCentral()
    maven { url  "http://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    maven { url "https://dl.bintray.com/kotlin/exposed/" }
    maven { url "https://plugins.gradle.org/m2/" }
}

shadowJar {
    baseName = 'ktor-lifelog'
    classifier = null
    version = null
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "io.ktor:ktor-server-core:$ktor_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-html-builder:$ktor_version"
    compile "ch.qos.logback:logback-classic:1.2.1"
    compile 'org.koin:koin-ktor:0.9.3'
    compile 'org.jetbrains.exposed:exposed:0.9.1'
    compile group: 'org.postgresql', name: 'postgresql', version: '9.4-1206-jdbc4'

    testCompile "io.ktor:ktor-server-test-host:$ktor_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "com.nhaarman:mockito-kotlin:1.5.0"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

tasks.test.finalizedBy jacocoTestReport

jacoco {
    toolVersion = "0.8.1"
}

jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.enabled = true
    }
    sourceDirectories = files('src/main')
}
